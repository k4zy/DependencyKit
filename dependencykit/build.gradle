apply plugin: 'com.android.library'
apply plugin: 'jacoco'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }
}

jacoco {
    // latest: 0.7.9 but not work with robolectric
    // ref: http://qiita.com/teracy/items/fee05cb68022f8241825
    toolVersion = "0.7.1+"
}

def coverageSourceDirs = ['src/main/java']
def coverageExcludeFiles = ['**/R.class', '**/R$*.class', '**/DataBindingInfo.class', '**/android/databinding/*.*', '**/com/android/**/*.*', '**/BR.class']


task jacoco(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/jacoco/html/${project.name}"
        classDirectories = files(
                fileTree(
                        dir: "${buildDir}/intermediates/classes/debug",
                        exclude: coverageExcludeFiles))
    }
    sourceDirectories = files(coverageSourceDirs)
    executionData = files "${buildDir}/jacoco/testDebugUnitTest.exec"

    doLast {
        println "jacoco html report has been generated to file://${reports.html.destination}/index.html"
    }
}

dependencies {
    compile 'javax.inject:javax.inject:1'
    testCompile 'junit:junit:4.12'
}
